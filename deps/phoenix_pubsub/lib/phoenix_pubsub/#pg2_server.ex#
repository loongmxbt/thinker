defmodule Phoenix.PubSub.PG2Server do
  @moduledoc false

  use GenServer
  alias Phoenix.PubSub.Local

  def start_link(server_name) do
    GenServer.start_link __MODULE__, server_name, name: server_name
  end

  def direct_broadcast(fastlane, server_name, pool_size, node_name, from_pid, topic, msg) do
    server_name
    |> get_members(node_name)
    |> do_broadcast(fastlane, server_name, pool_size, from_pid, topic, msg, [])
  end

  def broadcast(fastlane, server_name, pool_size, from_pid, topic, msg, opts) do
    server_name
    |> get_members()
    |> do_broadcast(fastlane, server_name, pool_size, from_pid, topic, msg, opts)
  end

  defp do_broadcast({:error, {:no_such_group, _}}, _fastlane, _server, _pool, _from, _topic, _msg, _opts) do
    {:error, :no_such_group}
  end
  defp do_broadcast(pids, fastlane, server_name, pool_size, from_pid, topic, msg, opts)
    when is_list(pids) do
    exclude_nodes = opts[:exclude_nodes] || []
    local_node = Phoenix.PubSub.node_name(server_name)

    Enum.each(pids, fn
      pid when is_pid(pid) and node(pid) == node() ->
        Local.broadcast(fastlane, server_name, pool_size, from_pid, topic, msg)
      pid when is_pid(pid) ->
        unless node(pid) in exclude_nodes do
          send(pid, {:forward_to_local, fastlane, from_pid, pool_size, topic, msg})
        end
      {^server_name, node_name} when node_name == local_node ->
        Local.broadcast(fastlane, server_name, pool_size, from_pid, topic, msg)
      {_server_name, node_name} = dest ->
        unless node_name in exclude_nodes do
          send(dest, {:forward_to_local, fastlane, from_pid, pool_size, topic, msg})
        end
    end)
    :ok
  end

  def init(server_name) do
    pg2_group = pg2_namespace(server_name)
    :ok = :pg2.create(pg2_group)
    :ok = :pg2.join(pg2_group, self)

    {:ok, server_name}
  end

  def handle_info({:forward_to_local, fastlane, from_pid, pool_size, topic, msg}, name) do
    # The whole broadcast will happen inside the current process
    # but only for messages coming from the distributed system.
    Local.broadcast(fastlane, name, pool_size, from_pid, topic, msg)
    {:noreply, name}
  end

  defp get_members(server_name) do
    :pg2.get_members(pg2_namespace(server_name))
  end
  defp get_members(server_name, node_name) do
    [{server_name, node_name}]
  end

  defp pg2_namespace(server_name), do: {:phx, server_name}
end
